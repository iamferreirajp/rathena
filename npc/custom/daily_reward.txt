//===== EinherjarRO Scripts ================================== 
//= Daily Prize, OnPCLoginEvent
//===== By: ================================================== 
//= Stolao
//===== Current Version: ===================================== 
//= 1.63
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= A reward system for players who play more frequently
//===== Comments: ============================================
//= Maybe Make .MinWait an array mins,days,weeks,months,years;
//===== Additional Comments: =================================
//= For Older See Old Versions
//= 1.51 Changed set .@g formula and move lower into script
//= 1.52 Replace all disbottom -> message
//= 1.53 Changed Format to Include minuets instead of just hours
//= 1.54 Made Time till next rewards display dynamic
//= 1.55 Changed a forgotten  .PointType$ -> getd(.@TT[1])
//= 1.56 changed .@XT -> .@XT$
//= 1.57 Added Atoi where nessisary
//= 1.58 Fixed a swapped .@x and .@x+1
//= 1.59 Serveral Edit Undocumented to fix
//= 1.5A Fixed a logic bug, day 1 skipped
//= 1.5B Fixed a [-1] bug
//= 1.5C Actualy Fixed a logic bug, day 1 skipped
//= 1.5D Added A .Rest Notification
//= 1.5E Moved Sleep2 after the day check
//= 1.5F Added additive logging so a dc wont reset the .Rest Delay
//= 1.60 Added timer break into @at and @afk
//= 1.61 Added '[Bônus Diário]:' to all messages
//= 1.62 Fixed missing "
//= 1.63 Fixed missing )
//===== Contact Info: ========================================
//= [Stolao] 
//= Email: Taingram11@gmail.com
//============================================================
-	script	LOGIN	-1,{
OnWhisperGlobal:
OnLoginCmnd:
OnPCLoginEvent:
	if(.Rest){
		set @login, (gettime(7) * 365 * 24 * 60) + (gettime(8) * 24 * 60) + (gettime(3) * 60) + gettime(2);
		if(@login > #LastDailyReward + .MaxWait ){
			set #logintimer, 0;
			set @login, 0;
		}
		set .@rest, (gettime(7) * 365 * 24 * 60) + (gettime(8) * 24 * 60) + (gettime(3) * 60) + .Rest;
		message strcharinfo(0),"[Bônus Diário]: Para receber a recompensa você deve permanecer online por "+ ( @login - .@rest ) +" minutos";
	}
	set .@i, (gettime(7) * 365 * 24 * 60) + (gettime(8) * 24 * 60) + (gettime(3) * 60) + gettime(2);
	if(#DRewardCon == 13){
		#DRewardCon = 1;
		#LastDailyReward = 0;
	}
	if(.@i - .Rest >= (#LastDailyReward + .MinWait)){
		sleep2 100 + ( .Rest * 60000 ) - ( .@i - #logintimer );
		if(.@i < #LastDailyReward + .MaxWait){	set #DRewardCon, #DRewardCon + 1;
		} else {	set #DRewardCon, 0;	}
		set .@g, (#DRewardCon > getarraysize(.Rewards$)) ? (getarraysize(.Rewards$)-1) : #DRewardCon;
		explode(.@XT$,.Rewards$[.@g],"|");
		for(set .@x,0; .@x < getarraysize(.@XT$); set .@x,.@x + 1){
			set .@TT[.@x], atoi(.@XT$[.@x]);
		}
		if(.Mode & 1 && .@TT[4]){	//[Note]: I need to figure out a checkweight for all items tired atm lol, for now ill leave it missing
			for(set .@x,4; .@x < getarraysize(.@TT); set .@x,.@x + 2){	getitem  .@TT[.@x], .@TT[.@x+1];	message strcharinfo(0),"[Bônus Diário]: Recebeu "+  .@TT[.@x+1] +" x "+ getitemname(.@TT[.@x]);	}
		}
		if(.Mode & 2){
			if(.@TT[0]){	set Zeny,Zeny + .@TT[0];	message strcharinfo(0),"[Bônus Diário]: "+ .@TT[0] +" Zenys recebidos.";	}
			if(.@TT[1]){	set getd(.Points$[0]),getd(.Points$[0]) + .@TT[1];	message strcharinfo(0),"[Bônus Diário]: "+ .@TT[1] +" "+.Points$[1];}
		}
		if(.Mode & 4 && (.@TT[3] || .@TT[4])) getexp .@TT[3], .@TT[4];
		if(.Mode & 8){
			for(set .@x,0; .@x < getarraysize(.BuffInfo); set .@x,.@x + 4){
				if(#DRewardCon % .BuffInfo[.@x + 1] == 0)
					sc_start .BuffInfo[.@x], .BuffInfo[.@x + 2] * 60000, .BuffInfo[.@x + 3];
			}
		}
		message strcharinfo(0),"[Bônus Diário]: Você recebeu a recompensa por "+#DRewardCon+" dia"+((#DRewardCon > 1)?"s":"")+" consecutivos.";
		cutin .Pic$[.@g],4;
		set #LastDailyReward, .@i;
		set #logintimer, 0;
		set @login, 0;
	} else {
		set .@days,(#LastDailyReward + .MinWait - .@i)/60/24;
		set .@hours,((#LastDailyReward + .MinWait - .@i)/60)%24;
		set .@mins,(#LastDailyReward + .MinWait - .@i)%60;
		message strcharinfo(0),"[Bônus Diário]: Você receberá sua recompensa em "+ ((.@days) ? .@days +" dias " : "") +""+ ((.@hours) ? .@hours +" horas " : "") +""+ ((.@mins) ? .@mins +" minutos " : "");
	}
end;
OnPCLogoutEvent:
	if(@login){
		set .@i, (gettime(7) * 365 * 24 * 60) + (gettime(8) * 24 * 60) + (gettime(3) * 60) + gettime(2);
		set #logintimer, .@i - @login + #logintimer;
		set @login, 0;
	}
return;
end;
OnInit:
	//Basic Settings
	//   1: Item | 2: Points | 4: Exp  
	//   8: Gain Buffs Every X Consecutive Days logged in
	//     (a bit value, e.g. 3 = Items & Points from Multi)
	set .Mode,3;

	//To Enable @ Command '@loginreward' unslash next lines
	// * Needs extra commands for typos
	bindatcmd("bonus","LOGIN::OnLoginCmnd",0,99);

	//Enable Delay if @afk or @autotrade are available to players
	//  remove // to enable certain command checks
	//bindatcmd("at"	,"LOGIN::OnPCLogoutEvent",0,99);
	//bindatcmd("autotrade"	,"LOGIN::OnPCLogoutEvent",0,99);
	//bindatcmd("AT"	,"LOGIN::OnPCLogoutEvent",0,99);
	//bindatcmd("Autotrade"	,"LOGIN::OnPCLogoutEvent",0,99);
	//bindatcmd("afk"	,"LOGIN::OnPCLogoutEvent",0,99);
	//bindatcmd("AFK"	,"LOGIN::OnPCLogoutEvent",0,99);
	

	//Minimum Minuets Between Collecting Daily Reward
	//   Day: 22*60 - 24*60
	//   Week: 10080
	set .MinWait,24*60;

	//Minuets Before Lose Consecutive Daily Reward
	//   Day: 48*60 - 50*60
	//   Week: 20160
	set .MaxWait,3000;

	//Number of mins after logging before collecting prize
	set .Rest,0;

	//Point Type
	//   [0] Points earned
	//   [1] Point name in mes
	setarray .Points$,"#resetpoint","Reset Points";

	// Consecutive Days Buff
	// Each buff contains 4 variables (32 Total Max)
	// <Type>,<Days>,<Duration>,<Rate>, // Buff 1
	// <Type>,<Days>,<Duration>,<Rate>, // Buff 2
	//   ...;
	//
	//  Example: 188,7,45,3
	//    -Every 7th consecutive day logged in Player gains +3 Str for 45 mins
	//
	//  Type is 188, which references which SC_ to use, SC_INCSTR in this example
	//     -For a full list of SC_ visit the db/const.txt
	//  Days is days buff is applied, in this example 7, so every 7th day, 14,21,28....
	//  Duration is buff duration is Minuits, in this example 45 mins
	//  Rate is buff strength, in this example player gains 3 Str
	setarray .BuffInfo	,260,2,360,1	// Life Insurance for 360 Mins Every 2nd Day
				,198,3,120,10	// +10% Hp for 120 Mins Every 3th Day
				,196,5,120,25	// +25 Flee for 120 Mins Every 5th Day
				,257,7,240,50;	// +50% Exp for 240 Mins Every 7th Day

	// Daily Prize items (max 128 days):
	//   "<Zeny>|<Points>|<BaseExp>|<JobExp>|<itemID-1>|<amount-1>|<itemID-2>|<amount-2>...etc", // Day 1
	//   "<Zeny>|<Points>|<BaseExp>|<JobExp>|<itemID-1>|<amount-1>|<itemID-2>|<amount-2>...etc"  // Day 2
	//   ...;
	// Total length of any days string must be 255 or shorter
	// * If players login longer than the last set
	//   day, they will keep getting the last prize.
	setarray .Rewards$,
		"10000|1|0|0|12208|1|14606|1",			// Day 1: 1 Manual de classe 50% e um manual job 25%		
		"12000|2|0|0|12263|1|14592|1",			// Day 2: 1 manual de classe 100% e um manual job 25%
		"13000|3|0|0|604|10",			// Day 3: 10 galhos secos
		"14000|4|0|0|12090|2",			// Day 4: 2 escorpião no vapor.
		"15000|5|0|0|12080|2",			// Day 5: 2 coquetel sopro do dragão.
		"16000|6|0|0|12205|2",			// Day 6: 2 Tônico de Hwergelmir.
		"17000|7|0|0|12202|2",			// Day 7: 2 língua no vapor.
		"18000|8|0|0|12206|2",			// Day 8: 2 nove caudas cozidas.
		"19000|9|0|0|12207|2",			// Day 9: 2 sopas da imortalidade.
		"20000|10|0|0|14232|1",			// Day 10: 1 caixa de frutos de yggdrasil[10].
		"21000|11|0|0|12210|1",			// Day 11: 1 goma de mascar.
		"22000|12|0|0|12214|1",			// Day 12: 1 espelho convexo.
		"23000|13|0|0|12103|1",			// Day 13: 1 galho sangrento.
		"25000|14|0|0|7491|1";			// Day 14: 1 Caixa de Equipamentos lvl 3

		
	setarray .Pic$,
	"day1",
	"day2", 
	"day3",
	"day4",
	"day5",
	"day6",
	"day7",
	"day8",
	"day9",
	"day10",
	"day11",
	"day12",
	"day13",
	"day14";
end;
}