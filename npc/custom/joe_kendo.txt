prt_in,57,54,3	script	Joe Kendo	4_M_DWARF,{
	disable_items;
	cutin "refineshadow",4;
	mes "[Joe Kendo]";
	mes "Você quer refinar um equipamento sombrio?";
	mes "Selecione o item que você deseja refinar.";
	next;
	setarray .@position$[0],"Armadura","Arma","Escudo","Sapatos","Acessório 1","Acessório 2";
	for (.@i=EQI_SHADOW_ARMOR; .@i<=EQI_SHADOW_ACC_L; .@i++){
		.@menu$ = .@menu$+((getequipisequiped(.@i))?getequipname(.@i):"^8C8C8C"+.@position$[.@i-EQI_SHADOW_ARMOR]+" [Não equipado]^000000")+":";
	}
	.@menu$ = .@menu$+"Informações do Refinamento";
	.@SelectedPart = select(.@menu$)+EQI_SHADOW_ARMOR-1;
	if (.@SelectedPart == EQI_SHADOW_ACC_L+1){	// Refine Info
		mes "[Joe Kendo]";
		mes "Os itens sombrios ganharão bônus e efeitos extras pelo nível de refinamento.";
		next;
		mes "[Joe Kendo]";
		mes "Os itens formam combos que utilizam o refinamento para serem ativados.";
		mes "Os bônus e efeitos estão descritos nos próprios itens, como por exemplo:";
		mes "Arma: A cada +1 de refinamento recebe +1 MATK.";
		mes "Ou: Armadura: A cada +1 de refinamento aumenta o HP em +10.";
		next;
		mes "[Joe Kendo]";
		mes "Você precisará de Oridecon ou Elunium e uma taxa de 20000 zeny para cada refinamento.";
		next;
		mes "[Joe Kendo]";
		mes "O nível de segurança para itens sombrios é +4.";
		mes "Portanto ao passar para +5 ou adiante os itens podem ser destruídos.";
		next;
		mes "Você poderá usar os minérios enriquecidos e perfeitos para aumentar suas chances.";
		mes "O nível máximo de equipamentos sombrios é +10.";
		close;
	}
	while(1){
		mes "[Joe Kendo]";
		mes "20000 zeny serão gastos para o pagamento do refinamento.";
		mes "Selecione o material para começarmos o processo:";
		next;
		.@index = 0;
		if (.@SelectedPart != EQI_SHADOW_WEAPON)
			.@index = 1;
		setarray .@s_weapon1[0],Oridecon,Elunium;
		setarray .@s_weapon2[0],Enriched_Oridecon,Enriched_Elunium;
		setarray .@s_weapon3[0],HD_Oridecon,HD_Elunium;
		if (countitem(.@s_weapon1[.@index]))
			.@mate$[0] = getitemname(.@s_weapon1[.@index]);
		else{
			.@mate$[0] = "^8C8C8C"+ getitemname(.@s_weapon1[.@index]) +"^000000";
			.@miss[0] = 1;
		}
		if (countitem(.@s_weapon2[.@index]))
			.@mate$[1] = getitemname(.@s_weapon2[.@index]);
		else{
			.@mate$[1] = "^8C8C8C"+ getitemname(.@s_weapon2[.@index]) +"^000000";
			.@miss[1] = 1;
		}
		if (getequiprefinerycnt(.@SelectedPart) > 6 && countitem(.@s_weapon3[.@index]))
			.@mate$[2] = getitemname(.@s_weapon3[.@index]);
		else {
			.@mate$[2] = "^8C8C8C"+ getitemname(.@s_weapon3[.@index]) +"^000000";
			.@miss[2] = 1;
		}
		//-----------------------------------------------------------------------------
		.@option = select("Cancelar",.@mate$[0],.@mate$[1],.@mate$[2]);
		if (.@option == 1){
			mes "[Joe Kendo]";
			mes "Você cancelou o refinamento.";
			close;
		}
		.@option -= 2;
		if (.@option==2){	//Minérios Perfeitos
			if (getequiprefinerycnt(.@SelectedPart) < 7){
				mes "[Joe Kendo]";
				mes "Só é possível usar minérios perfeitos com equipamentos de nível +7 ou superior.";
				close;
			}
			.@materialperfeito = 1;
		}
		if (.@miss[.@option]){
			mes "[Joe Kendo]";
			mes "Você não tem os materiais para esse refinamento.";
			close;
		}
		.@choose = getd(".@s_weapon"+(.@option+1)+"["+ .@index +"]");
		if (Zeny < 20000) {
			mes "[Joe Kendo]";
			mes "Você não tem Zeny suficiente.";
			mes "Volte quando tiver pelo menos o mínimo para o refinamento.";
			close;
		}
		if (getequiprefinerycnt(.@SelectedPart) > 9) {
			mes "[Joe Kendo]";
			mes "O refinamento de Equipamentos Sombrios só é possível até o nível +10.";
			mes "Não existem equipamentos destes acima deste nível.";
			close;
		}
		if (!getequipisenableref(.@SelectedPart)) {
			mes "[Joe Kendo]";
			mes "Este item não pode ser refinado.";
			close;
		}
		if (getequippercentrefinery(.@SelectedPart) < 100) {
			mes "[Joe Kendo]";
			mes "O limite de segurança para o refinamento dos Itens Sombrios é +4.";
			if (!.@materialperfeito) {
				mes "Se você tentar ir além, o item poderá ser quebrado se a tentativa falhar. Você deseja continuar?";
			}
			else {
				mes "Se você tentar ir além, o nível de refinamento do item pode decrescer se a tentativa falhar.";
				mes "Você deseja continuar?";
			}
			next;
			if (select("Continuar","Cancelar") == 2) {
				mes "[Joe Kendo]";
				mes "Você cancelou o refinamento.";
				close;
			}
		}
		//-----------------------------------------------------------------------------
		mes "[Joe Kendo]";
		mes "Vamos lá.!!";
		next;
		if (Zeny < 20000) {
			mes "[Joe Kendo]";
			mes "Você não tem Zeny suficiente.";
			close;
		}
		if (!countitem(.@choose)) {
			mes "[Joe Kendo]";
			mes "Você não tem "+ getitemname(.@choose) +" suficiente para a tentativa.";
			close;
		}
		delitem .@choose,1;
		Zeny -= 20000;
		if (getequippercentrefinery(.@SelectedPart) > rand(100) || getequippercentrefinery(.@SelectedPart) > rand(100) || getequippercentrefinery(.@SelectedPart) > rand(100)) {
			successrefitem .@SelectedPart;
			mes "[Joe Kendo]";
			mes "SUCESSO!!!";
			next;
		}
		else {
			if(.@materialperfeito){
				downrefitem .@SelectedPart;
				failedrefitem .@SelectedPart;
				mes "[Joe Kendo]";
				mes "Oh não, o refinamento falhou, me desculpe, mas você estava ciente dos riscos.";
				next;
			}else{
				failedrefitem .@SelectedPart;
				mes "[Joe Kendo]";
				mes "Oh não, o refinamento falhou, me desculpe, mas você estava ciente dos riscos.";
				close;
			}	
		}
	}
}
morocc_in,68,30,3	duplicate(Joe Kendo)	Joe Kendo#morocc	4_M_DWARF
payon,148,174,3	duplicate(Joe Kendo)	Joe Kendo#payon	4_M_DWARF
alberta_in,18,56,3	duplicate(Joe Kendo)	Joe Kendo#alberta	4_M_DWARF
yuno_in01,173,18,3	duplicate(Joe Kendo)	Joe Kendo#yuno	4_M_DWARF
ein_in01,24,82,3	duplicate(Joe Kendo)	Joe Kendo#einbroch	4_M_DWARF
lhz_in02,280,17,3	duplicate(Joe Kendo)	Joe Kendo#lighthalzen	4_M_DWARF
